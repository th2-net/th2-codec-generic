plugins {
    id "com.exactpro.th2.gradle.base" version "0.0.6"
    id "com.exactpro.th2.gradle.component" version "0.0.6" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.exactpro.th2.gradle.component'
    apply plugin: 'application'

    ext {
        resourceDir = "src/main/resources"

        sailfishVersion               = "3.3-SNAPSHOT"

        dockerCodecImplementationDir = 'codec_implementation'
    }

    ext.excludeSailfish = { rcd ->
        rcd.excludeModule("com.exactpro.sf", "sailfish-core")
        rcd.excludeModule("com.exactpro.sf", "sailfish-common")
        rcd.excludeModule("com.exactpro.sf", "sailfish-rest-api-client")
        rcd.excludeModule("com.exactpro.sf", "service-http")
    }

    repositories {
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            content {
                excludeSailfish(it)
            }
        }
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            metadataSources {
                mavenPom()
                artifact()
                ignoreGradleMetadataRedirection()
            }
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
            metadataSources {
                mavenPom()
                artifact()
                ignoreGradleMetadataRedirection()
            }
        }

        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
            content {
                excludeSailfish(it)
            }
        }

        mavenCentral()
        mavenLocal()

        configurations.configureEach {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    configurations {
        codec
    }

    dependencies {
        implementation("com.exactpro.sf:${sf_service_name}:${sailfishVersion}") {
            exclude module: 'sailfish-core'
            exclude module: 'sailfish-common'
            // FIXME: try to exclude codec-sailfish dependencies
            exclude module: 'kotlin-stdlib-jdk8'
            exclude module: 'kotlin-reflect'
            exclude module: 'kotlin-stdlib'
            exclude module: 'kotlin-stdlib-common'
        }
        implementation 'com.exactpro.th2:codec-sailfish:5.1.0-dev'
    }

    application {
        mainClass.set("com.exactpro.th2.codec.MainKt")
    }
}

dependencyCheck {
    suppressionFile = file('suppressions.xml')
}