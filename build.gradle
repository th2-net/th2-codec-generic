plugins {
    id 'com.palantir.docker' version '0.25.0' apply false
    id "org.owasp.dependencycheck" version "8.1.0"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.palantir.docker'

    ext {
        resourceDir = "src/main/resources"
        sharedDir = file("${project.rootDir}/shared")

        baseSailfishVersion           = '3.3'
        sailfishVersion               = "3.3-SNAPSHOT"

        dockerCodecImplementationDir = 'codec_implementation'
    }

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        }

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    configurations {
        codec
    }

    dependencies {
        codec ("com.exactpro.sf:${sf_service_name}:${sailfishVersion}") {
            exclude module: 'sailfish-core'
            exclude module: 'sailfish-common'
            // FIXME: try to exclude codec-sailfish depenendencies
            exclude module: 'kotlin-stdlib-jdk8'
            exclude module: 'kotlin-reflect'
            exclude module: 'kotlin-stdlib'
            exclude module: 'kotlin-stdlib-common'
        }
    }

    docker {
        copySpec.with {
            from(configurations.codec) {
                into dockerCodecImplementationDir
            }
            from(resourceDir) {
                into '.'
            }
        }
    }
}

dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS=5

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}
