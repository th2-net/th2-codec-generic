plugins {
    id 'com.palantir.docker' version '0.25.0' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.palantir.docker'

    ext {
        resourceDir = "src/main/resources"
        sharedDir = file("${project.rootDir}/shared")
        codecDataPath = "${buildDir}/codecData"
        sailfishVersion  = "${sailfish_version_major}.${sailfish_version_minor}"
        artifactVersion  = "${sailfishVersion}-SNAPSHOT"

        codecdockerImageVersion = "1.0.2.5" // FIXME
        dockerImageVersion = "${version_major}.${version_minor}.${version_maintenance}.${version_build}"
        dockerCodecImplementationDir = 'codec_implementation'
    }

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Artifactory'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    configurations {
        codec
    }

    dependencies {
        codec ("com.exactpro.sf:${sf_service_name}:${artifactVersion}") {
            exclude module: 'sailfish-core'
            exclude module: 'sailfish-common'
        }
    }

    docker {
        name "${target_docker_repository}/${docker_image_name}:${dockerImageVersion}"
        tag 'local-version', "${docker_image_name}:${dockerImageVersion}"

        buildArgs([SOURCE_DOCKER_VERSION: codecdockerImageVersion])

        copySpec.with {
            from(configurations.codec) {
                into dockerCodecImplementationDir
            }
            from(resourceDir) {
                into '.'
            }
        }
    }
}
